---
title: PROGMEM
categories: [ "Zmienne" ]
subCategories: [ "Narzędzia" ]
---





= PROGMEM


// POCZĄTEK SEKCJI OPISOWEJ
[#overview]
--

[float]
=== Opis
Przechowuje dane w pamięci flash (pamięci programu) zamiast w pamięci SRAM (pamięci danych). Opis przedstawiający różne http://www.arduino.cc/playground/Learning/Memory[typy pamięci] dostępne w płytkach Arduino.

Słowo kluczowe `PROGMEM` jest modyfikatorem zmiennej, należy go używać tylko z typami danych zdefiniowanymi w pgmspace.h. Mówi kompilatorowi "umieść tę informację w pamięci flash" zamiast w SRAM, gdzie normalnie zostało by to zapisane.

PROGMEM jest częścią biblioteki link:http://www.nongnu.org/avr-libc/user-manual/group__avr__pgmspace.html[pgmspace.h]. Jest on automatycznie dołączany do nowych wersji środowiska (IDE). Jeśli jednak używasz wersji IDE poniżej 1.0 (2011), najpierw musisz dołączyć bibliotekę u góry szkicu, tak jak poniżej:

`#include <avr/pgmspace.h>`
Chociaż `PROGMEM` może być użyty do pojedynczej zmiennej, tak naprawdę warto zamieszać tylko wtedy, gdy masz większy blok danych, który musi być przechowywany, co jest najwygodniejsze w przypadku tablicy (lub innej strukturze danych C++ nie opisywanych tutaj).

Użycie "PROGMEM" jest dwuetapowe. Po zapisaniu danych do pamięci flash trzeba użyć specjalnych metod (funkcji), również zdefiniowanych w bibliotece link:http://www.nongnu.org/avr-libc/user-manual/group__avr__pgmspace.html [pgmspace.h], aby odczytać dane z pamięci programu z powrotem do SRAM, abyśmy mogli zrobić z nim coś pożytecznego.
[%hardbreaks]


[float]
=== Składnia
`const TypDanych nazwaZmiennej[] PROGMEM = {dane0, dane1, dane3...};`

Ponieważ PROGMEM jest modyfikatorem zmiennej, nie ma jasnej i sztywnej reguły dotyczącej tego, jak powinno się go używać, więc kompilator Arduino akceptuje wszystkie poniższe definicje, jako synonimy. Jednak eksperymenty wykazały, że w różnych wersjach Arduino (zależnych od wersji GCC) PROGMEM może działać w jednym miejscu, a nie w innym. Poniższy przykład "tabeli ciągów" został przetestowany pod kątem współpracy z Arduino 13. Wcześniejsze wersje środowiska mogą działać lepiej, jeśli PROGMEM będzie dołączony po nazwie zmiennej.

`const typDanych nazwaZmiennej[] PROGMEM = {};  // użyj tej formy` +
`const PROGMEM typDanych nazwaZmiennej[] = {};  // lub tej` +
`const typDanych PROGMEM nazwaZmiennej[] = {};  // ale może nie tej`


[float]
=== Parametry
`typDanych`: Dozwolone typy danych: dowolny typ zmiennej. +
`nazwaZmiennej`: nazwa twojej tabeli danych.

--
// KONIEC SEKCJI OPISOWEJ




// POCZĄTEK SEKCJI JAK UŻYWAĆ
[#howtouse]
--

[float]
=== Przykładowy kod
// Poniżej dodaj przykładowy kod i opisz jego działanie   ►►►►► TA SEKCJA JEST OBOWIĄZKOWA ◄◄◄◄◄
Poniższe fragmenty kodu ilustrują, jak odczytywać i zapisywać znaki (bajty) oraz liczby całkowite bez znaku (2 bajty) do pamięci programu.

[source,arduino]
----
// zapisz kilka liczb całkowitych bez znaku
const PROGMEM uint16_t zestawZnakow[] = { 65000, 32796, 16843, 10, 11234};

// zapisz kilka znaków
const char podpis[] PROGMEM = {"JESTEM PREDATOR, NIEWIDOCZNY WOJOWNIK. STWORZONY PRZEZ DEPARTAMENT USA"};

unsigned int liczba;
char jedenZnak;


void setup() {
  Serial.begin(9600);
  while (!Serial);  // poczekaj na połączenie z portem szeregowym. Wymagane dla wbudowanych portów USB

  // ten kod przygotowawczy będzie wykonany tylko raz:
  // odczytaj 2-bajtową liczbę całkowitą
  for (byte k = 0; k < 5; k++) {
    liczba = pgm_read_word_near(zestawZnakow + k);
    Serial.println(liczba);
  }
  Serial.println();

  // odczytaj znak
  for (byte k = 0; k < strlen_P(podpis); k++) {
    jedenZnak = pgm_read_byte_near(podpis + k);
    Serial.print(jedenZnak);
  }

  Serial.println();
}

void loop() {
  // a to kod, który będzie wykonywany ciągle:
}

----
*Tablice ciągów*

Często wygodnie jest ustawić tablicę ciągów, pracując z dużą ilością tekstu, na przykład w projekcie z wyświetlaczem LCD. Ponieważ same łańcuchy są tablicami, jest to w rzeczywistości przykład tablicy dwuwymiarowej.

Są to zwykle duże struktury, dlatego często pożądane jest umieszczenie ich w pamięci programu. Poniższy kod ilustruje ten pomysł.

[source,arduino]
----
/*
  demonstracja napisów w PROGMEM
  Jak zapisywać i odczytywać tabelę ciągów w pamięci programu (flash).

  Podsumowanie informacji z:
  http://www.nongnu.org/avr-libc/user-manual/pgmspace.html

  Konfigurowanie tabeli (tablicy) ciągów w pamięci programu jest nieco
  skomplikowane, ale tutaj jest dobry szablon do naśladowania.

  Konfiguracja ciągów jest procesem dwuetapowym. Najpierw zdefiniuj ciągi.
*/

#include <avr/pgmspace.h>
const char napis_0[] PROGMEM = "Napis 0"; // "Napis 0" itd napisy do przechowania - zmień na odpowiednie.
const char napis_1[] PROGMEM = "Napis 1";
const char napis_2[] PROGMEM = "Napis 2";
const char napis_3[] PROGMEM = "Napis 3";
const char napis_4[] PROGMEM = "Napis 4";
const char napis_5[] PROGMEM = "Napis 5";


// Następnie ustaw tabelę, aby odwoływała się do twoich ciągów.

const char *const tabela_napisow[] PROGMEM = {napis_0, napis_1, napis_2, napis_3, napis_4, napis_5};

char bufor[30];  // upewnij się, że jest wystarczająco duży, aby pomieścić największy ciąg znaków

void setup() {
  Serial.begin(9600);
  while (!Serial);  // poczekaj na połączenie z portem szeregowym. Wymagane dla wbudowanych portów USB
  Serial.println("OK");
}


void loop() {
  /* Korzystanie z tabeli ciągów w pamięci programu wymaga użycia specjalnych funkcji w celu pobrania danych.
     Funkcja strcpy_P kopiuje ciąg z przestrzeni programu do ciągu w pamięci RAM („bufor”).
     Upewnij się, że łańcuch odbiorczy w pamięci RAM jest wystarczająco duży, aby pomieścić
      wszystko, co pobierasz z przestrzeni programu. */


  for (int i = 0; i < 6; i++) {
    strcpy_P(bufor, (char *)pgm_read_word(&(tabela_napisow[i])));  // Niezbędne rzutowania i dereferencje, po prostu skopiuj.
    Serial.println(bufor);
    delay(500);
  }
}
----
[%hardbreaks]

[float]
=== Uwagi i ostrzeżenia
Należy pamiętać, że zmienne muszą być albo zdefiniowane globalnie, albo za pomocą słowa kluczowego static, aby pracować z PROGMEM.

Poniższy kod NIE będzie działał wewnątrz funkcji:

[source,arduino]
----
const char dlugi_napis[] PROGMEM = "Cześć, chciałbym powiedzieć trochę o sobie.\n";
----

Poniższy kod ZADZIAŁA, nawet jeśli będzie lokalnie zdefiniowany w funkcji:

[source,arduino]
----
const static char dlugi_napis[] PROGMEM = "Cześć, chciałbym powiedzieć trochę o sobie.\n"
----

[float]
=== Makrodefinicja `F()`

jeśli zastosowana zostanie następująca instrukcja:

[source,arduino]
----
Serial.print("Wypisz coś w monitorze szeregowym");
----

ciąg do wydrukowania jest zwykle zapisywany w pamięci RAM. Jeśli twój szkic drukuje wiele rzeczy na monitorze szeregowym, możesz łatwo przepełnić pamięć RAM. Jeśli masz wolne miejsce w pamięci FLASH, możesz łatwo wskazać, że ciąg musi być zapisany w tej pamięci, używając składni:

[source,arduino]
----
Serial.print(F("Wypisz coś na monitorze szeregowym, co jest przechowywane w pamięci FLASH"));
----

--
// KONIEC SEKCJI JAK UŻYWAĆ

// POCZĄTEK SEKCJI ZOBACZ TAKŻE
[#see_also]
--

[float]
=== Zobacz także

[role="example"]
* #EXAMPLE# https://www.arduino.cc/playground/Learning/Memory[Typy pamięci dostępne w Arduino^]

[role="definition"]
* #DEFINITION# link:../../data-types/array[array]
* #DEFINITION# link:../../data-types/string[string]

--
// KONIEC SEKCJI ZOBACZ TAKŻE
