---
title: Serial.print()
---




= print()


// POCZĄTEK SEKCJI OPISOWEJ
[#overview]
--

[float]
=== Opis
Drukuje dane do portu szeregowego jako czytelny dla człowieka tekst ASCII. To polecenie może przybierać różne formy. Liczby są drukowane przy użyciu znaku ASCII dla każdej cyfry. Liczby zmiennoprzecinkowe podobnie - jako cyfry ASCII, domyślnie z dwoma miejscami po przecinku. Bajty są wysyłane jako pojedynczy znak. Znaki i łańcuchy są wysyłane bez zmian. Na przykład:

* `Serial.print(78)` daje "78" +
* `Serial.print(1.23456)` daje "1.23" +
* `Serial.print('N')` daje "N" +
* `Serial.print("Hello world.")` daje "Hello world."


Opcjonalny drugi parametr określa używany podstawę systemu liczbowego (lub format); dozwolone wartości to `BIN(dwójkowy, lub podstawa 2)`, `OCT(ósemkowy, lub podstawa 8)`, `DEC(dziesiętny, lub podstawa 10)`, `HEX(szesnastkowy, lub podstawa 16)`. W przypadku liczb zmiennoprzecinkowych ten parametr określa liczbę używanych miejsc dziesiętnych. Na przykład:

* `Serial.print(78, BIN)` daje "1001110" +
* `Serial.print(78, OCT)` daje "116" +
* `Serial.print(78, DEC)` daje "78" +
* `Serial.print(78, HEX)` daje "4E" +
* `Serial.print(1.23456, 0)` daje "1" +
* `Serial.print(1.23456, 2)` daje "1.23" +
* `Serial.print(1.23456, 4)` daje "1.2346"

Możesz przekazać ciągi oparte na pamięci flash do Serial.print(), opakowując je za pomocą link:../../../../variables/utilities/progmem[F()]. Na przykład:

`Serial.print(F(“Hello World”))`

Aby przesłać dane bez konwersji do postaci znaków, użyj link:../write[Serial.write()].
[%hardbreaks]


[float]
=== Składnia
`_Serial_.print(wart)` +
`_Serial_.print(wart, format)`


[float]
=== Parametry
`_Serial_`: obiekt portu szeregowego. Zobacz listę dostępnych portów szeregowych dla każdej płytki na link:../../serial[stronie głównej klasy Serial]. +
`wart`: wartość do wydrukowania. Dozwolone typy danych: dowolny typ danych.


[float]
=== Wartości zwracane
`print()` zwraca liczbę zapisanych bajtów, chociaż odczyt tej liczby jest opcjonalny. Typ danych: `size_t`.

--
// KONIEC SEKCJI OPISOWEJ




// POCZĄTEK SEKCJI JAK UŻYWAĆ
[#howtouse]
--

[float]
=== Program przykładowy
// Poniżej dodaj przykładowy program i opisz jego działanie   ►►►►► TA SEKCJA JEST OBOWIĄZKOWA ◄◄◄◄◄


[source,arduino]
----
/*
  Użycie pętli for do drukowania liczb w różnych formatach.
*/
void setup() {
  Serial.begin(9600); // otwórz port szeregowy przy prędkości 9600 bps:
}

void loop() {
  // drukowanie etykiet
  Serial.print("BEZ FORMATU");  // drukuj etykietę
  Serial.print("\t");           // drukuj znak tabulacji

  Serial.print("DEC");
  Serial.print("\t");

  Serial.print("HEX");
  Serial.print("\t");

  Serial.print("OCT");
  Serial.print("\t");

  Serial.print("BIN");
  Serial.println();        // znak powrotu karetki po ostatniej etykiecie

  for (int x = 0; x < 64; x++) { // tylko część tablicy ASCII, zmień na odpowiedni
    // drukowanie w wielu formatach:
    Serial.print(x);       // drukuj jako liczbę dziesiętną zakodowaną w ASCII - to samo co "DEC"
    Serial.print("\t\t");  // drukuj dwa tabulatory, aby dopasować długość etykiety

    Serial.print(x, DEC);  // drukuj jako liczbę dziesiętną zakodowaną w ASCII
    Serial.print("\t");    // drukuj tabulator

    Serial.print(x, HEX);  // drukuj jako liczbę szesnastkową zakodowaną w ASCII
    Serial.print("\t");    // drukuj tabulator

    Serial.print(x, OCT);  // drukuj jako liczbę ósemkową zakodowaną w ASCII
    Serial.print("\t");    // drukuj tabulator

    Serial.println(x, BIN);  // drukuj jako liczbę dwójkową zakodowaną w ASCII
    // następnie dodaj znak powrotu karetki w "println"
    delay(200);            // opóźnienie 200 milisekund
  }
  Serial.println();        // drukuj kolejny znak powrotu karetki
}
----
[%hardbreaks]

[float]
=== Uwagi i Ostrzeżenia
Informacje na temat asynchroniczności `Serial.print()` można znaleźć w sekcji Uwagi i ostrzeżenia na link:../write#howtouse[stronie dokumentacji Serial.write()].

--
// KONIEC SEKCJI JAK UŻYWAĆ


// POCZĄTEK SEKCJI ZOBACZ TAKŻE
[#see_also]
--

[float]
=== Zobacz także

[role="language"]
* #JĘZYK# link:../begin[begin()]
* #JĘZYK# link:../end[end()]
* #JĘZYK# link:../available[available()]
* #JĘZYK# link:../read[read()]
* #JĘZYK# link:../peek[peek()]
* #JĘZYK# link:../flush[flush()]
* #JĘZYK# link:../println[println()]
* #JĘZYK# link:../write[write()]
* #JĘZYK# link:../serialevent[SerialEvent()]
* #JĘZYK# link:https://www.arduino.cc/en/Tutorial/Memory[Memory]

--
// KONIEC SEKCJI ZOBACZ TAKŻE
