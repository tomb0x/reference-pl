---
title: shiftOut()
categories: [ "Funkcje" ]
subCategories: [ "Zaawansowane We/Wy" ]
---

= shiftOut()


// POCZĄTEK SEKCJI OPISOWEJ
[#overview]
--

[float]
=== Opis
Shifts out a byte of data one bit at a time. Starts from either the most (i.e. the leftmost) or least (rightmost) significant bit. Each bit is written in turn to a data pin, after which a clock pin is pulsed (taken high, then low) to indicate that the bit is available.

Note- if you're interfacing with a device that's clocked by rising edges, you'll need to make sure that the clock pin is low before the call to `shiftOut()`, e.g. with a call to `digitalWrite(clockPin, LOW)`.

This is a software implementation; see also the https://www.arduino.cc/en/Reference/SPI[SPI library], which provides a hardware implementation that is faster but works only on specific pins.
[%hardbreaks]


[float]
=== Składnia
`shiftOut(dataPin, clockPin, bitOrder, value)`


[float]
=== Parametry
`dataPin`: the pin on which to output each bit. Allowed data types: `int`. +
`clockPin`: the pin to toggle once the dataPin has been set to the correct value. Allowed data types: `int`. +
`bitOrder`: which order to shift out the bits; either MSBFIRST or LSBFIRST. (Most Significant Bit First, or, Least Significant Bit First). +
`value`: the data to shift out. Allowed data types: `byte`.


[float]
=== Wartości zwracane
Nothing

--
// KONIEC SEKCJI OPISOWEJ




// POCZĄTEK SEKCJI JAK UŻYWAĆ
[#howtouse]
--

[float]
=== Przykładowy kod
// Opisz, na czym polega przykładowy kod i dodaj odpowiedni kod ►►►►► NINIEJSZA SEKCJA JEST OBOWIĄZKOWA ◄◄◄◄◄
Obwód do tego przykładu znajduje się na https://arduino.cc/en/Tutorial/ShiftOut[samouczek na temat sterowania rejestrem przesuwnym 74HC595].

[source,arduino]
----
//**************************************************************//
//  Nazwa   : shiftOutCode, Hello World                         //
//  Autorzy : Carlyn Maw,Tom Igoe                               //
//  Data    : 25 Paż, 2006                                      //
//  Wersja  : 1.0                                               //
//  Opis    : Kod użycia rejestru przesuwnego 74HC595           //
//          : do liczenia od 0 do 255                           //
//****************************************************************

//Styk podłączony do ST_CP układu 74HC595
int stykZatrzask = 8;
//Styk podłączony do SH_CP układu 74HC595
int stykZegarowy = 12;
//Styk podłączony do DS układu 74HC595
int stykDanych = 11;

void setup() {
  //ustaw styki na wyjściowe ponieważ są one adresowane w pętli głównej
  pinMode(stykZatrzask, OUTPUT);
  pinMode(stykZegarowy, OUTPUT);
  pinMode(stykDanych, OUTPUT);
}

void loop() {
  //procedura licząca
  for (int j = 0; j < 256; j++) {
    //ustaw w stan niski stykZatrzask i tak utrzymuj jak długo trwa transmisja
    digitalWrite(stykZatrzask, LOW);
    shiftOut(stykDanych, stykZegarowy, LSBFIRST, j);
    //ustaw stan wysoki styku zatrzasku, aby zasygnalizować układowi,
    //że nie musi już odbierać informacji
    digitalWrite(stykZatrzask, HIGH);
    delay(1000);
  }
}
----
[%hardbreaks]

[float]
=== Uwagi i ostrzeżenia
DataPin i clockPin muszą być już skonfigurowane jako wyjścia przez wywołanie link:../../digital-io/pinmode[pinMode()].

shiftOut został napisywany aby wysłać 1 bajt (8 bitów), więc aby wysłać wartości większe niż 255 będzie wymagać operacji dwuetapowej.
[source,arduino]
----
// Dla pierwszego najbardziej znaczącego bitu
int dane = 500;
// wyślij bardziej znaczący bajt
shiftOut(stykDanych, zegar, MSBFIRST, (dane >> 8));
// wyślij mniej znaczący bajt
shiftOut(stykDanych, zegar, MSBFIRST, dane);

// lub dla najmniej znaczącego bitu jako pierwszego
dane = 500;
// wyślij mniej znaczący bajt
shiftOut(stykDanych, zegar, LSBFIRST, dane);
// wyślij bardziej znaczący bajt
shiftOut(stykDanych, zegar, LSBFIRST, (dane >> 8));
----
[%hardbreaks]

--
// KONIEC SEKCJI JAK UŻYWAĆ


// POCZĄTEK SEKCJI ZOBACZ TAKŻE
[#see_also]
--

[float]
=== Zobacz także

--
// KONIEC SEKCJI ZOBACZ TAKŻE
