---
title: shiftOut()
categories: [ "Functions" ]
subCategories: [ "Advanced I/O" ]
---

= shiftOut()


// POCZĄTEK SEKCJI OPISOWEJ
[#overview]
--

[float]
=== Opis
Wysyła bajt danych bit po bicie. Zaczyna od najbardziej znaczącego (tj. najbardziej po lewej) lub najmniej (najbardziej po prawej) znaczącego bitu. Każdy bit wysyłany jest kolejno na styk danych, po impulsie na styku zegarowym (ustawiany stan wysoki, a następnie niski), aby wskazać, że bit jest dostępny.

Jeśli łączysz się z urządzeniem, które jest taktowane zboczami narastającymi, musisz upewnić się, że styk zegarowy jest w stanie niskim przed pierwszym wywołaniem `shiftOut()`, np. w połączeniu z `digitalWrite (stykZegarowy, LOW)`.

To jest implementacja programowa; Arduino zapewnia również link: https://www.arduino.cc/en/Reference/SPI [bibliotekę SPI], która korzysta z implementacji sprzętowej, która jest szybsza, ale działa tylko na określonych stykach.

[%hardbreaks]


[float]
=== Składnia
`shiftOut(stykDanych, stykZegarowy, kolejnośćBitów, wartość)`


[float]
=== Parametry
`stykDanych`: styk na którego należy wyprowadzić każdy bit. Dozwolone typy danych: `int`. +
`stykZegarowy`: styk przełączający się po ustawieniu stykuDanych na prawidłową wartość. Dozwolone typy danych: `int`. +
`kolejnośćBitów`: kolejność przesuwania bitów; *MSBFIRST* lub *LSBFIRST*. (Most Significant Bit First - pierwszy bit najbardziej znaczący, lub, Least Significant Bit First - pierwszy bit najmniej znaczący). +
`wartość`: dane do wysłania. Dozwolone typy danych: `byte`.


[float]
=== Wartości zwracane
Nic

--
// KONIEC SEKCJI OPISOWEJ




// POCZĄTEK SEKCJI JAK UŻYWAĆ
[#howtouse]
--

[float]
=== Przykładowy kod
// Opisz, na czym polega przykładowy kod i dodaj odpowiedni kod ►►►►► NINIEJSZA SEKCJA JEST OBOWIĄZKOWA ◄◄◄◄◄
Obwód do tego przykładu znajduje się na https://arduino.cc/en/Tutorial/ShiftOut[samouczek na temat sterowania rejestrem przesuwnym 74HC595].

[source,arduino]
----
//**************************************************************//
//  Nazwa   : shiftOutCode, Hello World                         //
//  Autorzy : Carlyn Maw,Tom Igoe                               //
//  Data    : 25 Paż, 2006                                      //
//  Wersja  : 1.0                                               //
//  Opis    : Kod użycia rejestru przesuwnego 74HC595           //
//          : do liczenia od 0 do 255                           //
//****************************************************************

//Styk podłączony do ST_CP układu 74HC595
int stykZatrzask = 8;
//Styk podłączony do SH_CP układu 74HC595
int stykZegarowy = 12;
//Styk podłączony do DS układu 74HC595
int stykDanych = 11;

void setup() {
  //ustaw styki na wyjściowe ponieważ są one adresowane w pętli głównej
  pinMode(stykZatrzask, OUTPUT);
  pinMode(stykZegarowy, OUTPUT);
  pinMode(stykDanych, OUTPUT);
}

void loop() {
  //procedura licząca
  for (int j = 0; j < 256; j++) {
    //ustaw w stan niski stykZatrzask i tak utrzymuj jak długo trwa transmisja
    digitalWrite(stykZatrzask, LOW);
    shiftOut(stykDanych, stykZegarowy, LSBFIRST, j);
    //ustaw stan wysoki styku zatrzasku, aby zasygnalizować układowi,
    //że nie musi już odbierać informacji
    digitalWrite(stykZatrzask, HIGH);
    delay(1000);
  }
}
----
[%hardbreaks]

[float]
=== Uwagi i ostrzeżenia
DataPin i clockPin muszą być już skonfigurowane jako wyjścia przez wywołanie link:../../digital-io/pinmode[pinMode()].

shiftOut został napisywany aby wysłać 1 bajt (8 bitów), więc aby wysłać wartości większe niż 255 będzie wymagać operacji dwuetapowej.
[source,arduino]
----
// Dla pierwszego najbardziej znaczącego bitu
int dane = 500;
// wyślij bardziej znaczący bajt
shiftOut(stykDanych, zegar, MSBFIRST, (dane >> 8));
// wyślij mniej znaczący bajt
shiftOut(stykDanych, zegar, MSBFIRST, dane);

// lub dla najmniej znaczącego bitu jako pierwszego
dane = 500;
// wyślij mniej znaczący bajt
shiftOut(stykDanych, zegar, LSBFIRST, dane);
// wyślij bardziej znaczący bajt
shiftOut(stykDanych, zegar, LSBFIRST, (dane >> 8));
----
[%hardbreaks]

--
// KONIEC SEKCJI JAK UŻYWAĆ


// POCZĄTEK SEKCJI ZOBACZ TAKŻE
[#see_also]
--

[float]
=== Zobacz także

--
// KONIEC SEKCJI ZOBACZ TAKŻE
