---
title: Mouse.move()
---




= Mouse.move()


// POCZĄTEK SEKCJI OPISOWEJ
[#overview]
--

[float]
=== Opis
Przesuwa kursor na podłączonym komputerze. Ruch na ekranie jest zawsze względem aktualnego położenia kursora. Przed użyciem `Mouse.move()` musisz wywołać link:../mousebegin[Mouse.begin()]
[%hardbreaks]


[float]
=== Składnia
`Mouse.move(xVal, yVal, kółko)`


[float]
=== Parametry
`xVal`: odległość do przesunięcia wzdłuż osi X. Dozwolone typy danych: `signed char`. +
`yVal`: odległość do przesunięcia wzdłuż osi y. Dozwolone typy danych: `signed char`. +
`kółko`: ilość przesunięcia kółka myszy. Dozwolone typy danych: `signed char`.


[float]
=== Wartości zwracane
Nic

--
// KONIEC SEKCJI OPISOWEJ




// POCZĄTEK SEKCJI JAK UŻYWAĆ
[#howtouse]
--

[float]
=== Program przykładowy
// Poniżej dodaj przykładowy program i opisz jego działanie   ►►►►► TA SEKCJA JEST OBOWIĄZKOWA ◄◄◄◄◄


[source,arduino]
----
#include <Mouse.h>

const int osX = A1;         // czujnik analogowy dla osi X
const int osY = A2;         // czujnik analogowy dla osi Y

int zakres = 12;              // zakres wyjściowy ruchu X lub Y
int przerwa = 2;              // opóźnienie odpowiedzi myszy w milisekundach
int granica = zakres / 4;     // próg spoczynkowy
int centrum = zakres / 2;     // wartość pozycji spoczynkowej
int minimum[] = {1023, 1023}; // rzeczywiste minimalne wartości {x, y} z analogRead 
int maximum[] = {0, 0};       // rzeczywiste maksymalne wartości {x, y} z analogRead 
int osie[] = {osX, osY};      // numery styków dla {x, y}
int mouseReading[2];          // końcowe odczyty myszy dla {x, y}


void setup() {
  Mouse.begin();
}

void loop() {
  // odczytaj i przeskaluj dwie osie:
  int odczytX = odczytOsi(0);
  int odczytY = odczytOsi(1);

  // porusz myszą:
  Mouse.move(odczytX, odczytY, 0);
  delay(przerwa);
}

/*
  odczytuje oś (0 lub 1 dla x lub y) i skaluje
   zakres wejścia analogowego w zakresie od 0 do <zakres>
*/

int odczytOsi(int numerOsi) {
  int dystans = 0; // odległość od środka zakresu wyjściowego

  // odczyt wejścia analogowego:
  int odczyt = analogRead(osie[numerOsi]);

  // aktualny odczyt przekracza max lub min dla tej osi,
  // ponownie ustaw maksimum lub minimum:
  if (odczyt < minimum[numerOsi]) {
    minimum[numerOsi] = odczyt;
  }
  if (odczyt > maximum[numerOsi]) {
    maximum[numerOsi] = odczyt;
  }

  // odwzoruj odczyt z zakresu wejścia analogowego na zakres wyjściowy:
  odczyt = map(odczyt, minimum[numerOsi], maximum[numerOsi], 0, zakres);

  // jeśli odczyt z wyjścia jest poza progiem pozycji spoczynkowej,
  // to użyj go
  if (abs(odczyt - centrum) > granica) {
    dystans = (odczyt - centrum);
  }

  // oś Y musi zostać odwrócona, aby
  // poprawnie odwzorować ruch
  if (numerOsi == 1) {
    dystans = -dystans;
  }

  // zwraca przebytą odległość dla tej osi:
  return dystans;
}
----
[%hardbreaks]

[float]
=== Uwagi i ostrzeżenia
Kiedy używasz polecenia `Mouse.move()`, Arduino przejmuje twoją mysz! Upewnij się, że masz nad tym kontrolę, zanim użyjesz tego polecenia. Może być przydatny klawisz przełączający kontrolę nad myszą.

--
// KONIEC SEKCJI JAK UŻYWAĆ


// POCZĄTEK SEKCJI ZOBACZ TAKŻE
[#see_also]
--

[float]
=== Zobacz także

[role="language"]
* #JĘZYK# link:../mouseclick[Mouse.click()]
* #JĘZYK# link:../mouseend[Mouse.end()]
* #JĘZYK# link:../mousepress[Mouse.press()]
* #JĘZYK# link:../mouserelease[Mouse.release()]
* #JĘZYK# link:../mouseispressed[Mouse.isPressed()]

--
// KONIEC SEKCJI ZOBACZ TAKŻE
